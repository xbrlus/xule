
constant $TAXONOMY_YEAR = '2023'
constant $PACKAGE_NAME = "Meta-" + $TAXONOMY_YEAR 

output create_package
true
package-name $PACKAGE_NAME
package-url 'https://taxonomies.xbrl.us/us-gaap/' + $TAXONOMY_YEAR + '/meta/'
document-uri 'us-gaap-metaModel-entire-' + $TAXONOMY_YEAR + '.xsd'
document-namespace 'http://fasb.org/us-gaap-metaModel-entire/' + $TAXONOMY_YEAR + ''
/**document-import list('https://www.xbrl.org/lrr/arcrole/accounting-arcrole-2023-01-04.xsd')**/
import2-url 'https://www.xbrl.org/lrr/arcrole/accounting-arcrole-2023-01-04.xsd'
document-package-entry-point true
document-package-entry-point-description 'META MODEL FOR THE US_GAAP ' + $TAXONOMY_YEAR + ' Taxonomy '
document-package-entry-point-language 'en'
document-package-entry-point-name 'Meta Model ' + $TAXONOMY_YEAR + ''

output create_main_schema
true
package-name $PACKAGE_NAME
document-uri 'us-gaap-metaModel-' + $TAXONOMY_YEAR + '.xsd'
document-imported-in 'us-gaap-metaModel-entire-' + $TAXONOMY_YEAR + '.xsd'
document-namespace 'http://fasb.org/us-gaap-metaModel/' + $TAXONOMY_YEAR + ''
document-import list('https://xbrl.fasb.org/us-gaap/' + $TAXONOMY_YEAR + '/elts/us-gaap-' + $TAXONOMY_YEAR + '.xsd','http://www.xbrl.org/2008/generic-link.xsd').to-xodel 
document-package-entry-point true
document-package-entry-point-description 'META MODEL SCHEMA'
document-package-entry-point-language 'en'
document-package-entry-point-name 'Meta Model Schema ' + $TAXONOMY_YEAR + ''

output create_types

true

package-name $PACKAGE_NAME
document-uri 'us-gaap-metaModel-' + $TAXONOMY_YEAR + '.xsd'
type-name qname('http://fasb.org/us-gaap-metaModel/' + $TAXONOMY_YEAR + '','trait').to-xodel
type-parent-name qname('http://www.xbrl.org/2003/instance','stringItemType').to-xodel


output create_concepts
$metaConcepts = filter $META.concepts where $item.name.namespace-uri == 'http://fasb.org/us-gaap-metaModel/2024'
for $concept in $metaConcepts
        $concept

package-name $PACKAGE_NAME
document-uri  'us-gaap-metaModel-' + $TAXONOMY_YEAR + '.xsd'
document-namespace 'http://fasb.org/us-gaap-metaModel/' + $TAXONOMY_YEAR + ''
concept-namespace  'http://fasb.org/us-gaap-metaModel/' + $TAXONOMY_YEAR + ''
concept-data-type (if $concept.data-type.name.local-name == 'trait' qname('http://fasb.org/us-gaap-metaModel/' + $TAXONOMY_YEAR + '','trait') else $concept.data-type.name).to-xodel
concept $concept.to-xodel

output make_tax_available
true
package-name  $PACKAGE_NAME
import2-url 'https://xbrl.fasb.org/us-gaap/' + $TAXONOMY_YEAR + '/elts/us-gaap-' + $TAXONOMY_YEAR + '.xsd'

output create_roles
$metaRoles = filter $META.roles where $item.uri == 'http://xbrl.us/metaModel/role'
for $role in $metaRoles.to-set
    $role

package-name  $PACKAGE_NAME
document-uri 'us-gaap-metaModel-' + $TAXONOMY_YEAR + '.xsd'
role $role.to-xodel

output create_arcrole_dim_equiv
true

package-name  $PACKAGE_NAME
document-uri 'accounting-arcroles-2023.xsd'
document-namespace 'http://fasb.org/2023/arcrole/accounting'
document-imported-in 'us-gaap-metaModel-entire-' + $TAXONOMY_YEAR + '.xsd'
arcrole-uri 'http://www.xbrl.org/2023/arcrole/concept-dimensional-equivalent'
arcrole-used-on list('Definition').to-xodel
arcrole-definition 'Indicate the equivalent line item axis and member that represents the fact value of another concept.'
arcrole-cycles-allowed 'undirected'
arcrole-id 'concept-dimensional-equivalent'

output create_arcrole_trait-concept
true

package-name  $PACKAGE_NAME
document-uri 'accounting-arcroles-2023.xsd'
document-namespace 'http://fasb.org/2023/arcrole/accounting'
document-imported-in 'us-gaap-metaModel-entire-' + $TAXONOMY_YEAR + '.xsd'
arcrole-uri 'http://www.xbrl.org/2021/arcrole/trait-concept'
arcrole-used-on list('Definition').to-xodel
arcrole-definition 'Indicates the relationship between a source duration trait domain member and a target concept. Trait concepts are abstract concepts with a domain item type. Trait-concept relationships describe the qualities or states of being of a target concept. Trait source concepts define a single trait such as Operating, Financing, Investing, Current, Noncurrent, Estimated Accrual, Continuing, Discontinued etc. The arc allows specific accounting or non-accounting traits to be associated with a target concept using the definition linkbase, thus allowing the attributes to be explicitly stated rather than being included as adjectives in the concepts name or label. This arc can be used in conjunction with the class-subclass arcrole. A subclass of a class concept assigned a trait will inherit that trait. For example if concept "Cash" is the target of a trait-concept arc with a source concept of "Current", then the subclass of the "Cash" concept "Cash In Bank" would also inherit the "Current" trait.'
arcrole-cycles-allowed 'undirected'
arcrole-id 'trait-concept'



output create_relationships
$metaNetworks = filter $META.networks where $item.role.uri == 'http://xbrl.us/metaModel/role'

for $metaRelationships in $metaNetworks.relationships 
    for $relationship in $metaRelationships
        $sourceNamespace = $relationship.source-name.namespace-uri.substring(1,last-index-of($relationship.source-name.namespace-uri,'4') - 1) + '3';
        $targetNamespace = $relationship.target-name.namespace-uri.substring(1,last-index-of($relationship.target-name.namespace-uri,'4') - 1) + '3';
        $role-short-name = (split($relationship.arcrole-uri,'/')).last
        $location = 'us-gaap-' +  $role-short-name + '-' + $TAXONOMY_YEAR + '.xml';
        $docNamespace = 'http://fasb.org/us-gaap-' + (split($relationship.arcrole-uri,'/')).last + '/' + $TAXONOMY_YEAR + ''
        $arcrole-uri = if $relationship.arcrole-uri == 'http://xbrl.org/int/dim/arcrole/domain-member' or $relationship.arcrole-uri == 'http://www.xbrl.org/2021/arcrole/trait-concept'
                            $relationship.arcrole-uri
                       else
                            'http://www.xbrl.org/2023/arcrole/' + $role-short-name
        $relationship

package-name $PACKAGE_NAME
document-uri $location 
document-namespace  $docNamespace
document-imported-in 'us-gaap-metaModel-entire-' + $TAXONOMY_YEAR + '.xsd'
relationship-source  qname($sourceNamespace, $relationship.source-name.local-name).to-xodel
relationship-target  qname($targetNamespace, $relationship.target-name.local-name).to-xodel
relationship-arcrole  $arcrole-uri
relationship $relationship.to-xodel

output create_labels

$metaConcepts = filter $META.concepts where $item.name.namespace-uri == 'http://fasb.org/us-gaap-metaModel/2024'
for $concept in $metaConcepts
    $conceptNamespace = 'http://fasb.org/us-gaap-metaModel/' + $TAXONOMY_YEAR + ''
    $conceptLocalName = $concept.name.local-name
    $newConcept = qname( $conceptNamespace , $conceptLocalName )
    for $label in ($concept.all-labels)
        $label
                                    
package-name $PACKAGE_NAME
document-uri 'us-gaap-metaModel-' + $TAXONOMY_YEAR + '-lab.xml'
document-imported-in 'us-gaap-metaModel-' + $TAXONOMY_YEAR + '.xsd'
label-concept-name $newConcept.to-xodel
label-text $label.text
label-role $label.role
label-lang $label.lang