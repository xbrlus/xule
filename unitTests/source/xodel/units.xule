namespace currency = https://xbrl.org/taxonomy/int/currency/current/CR/2023-08-23
namespace link = http://www.xbrl.org/2003/linkbase
namespace unit = http://www.xbrl.org/2023/units
namespace utr = http://www.xbrl.org/2009/utr
namespace si = http://www.xbrl.org/2023/si-prefix
namespace property-part = https://xbrl.org/taxonomy/int/codelist-common/CR/2023-08-23/property-part
namespace property = http://xbrl.org/2022/role/property
namespace dtr = http://www.xbrl.org/dtr/type/2022-03-31

/** EXTRACT DATA FROM UNITS REGISTRY 
-------------------------------------**/
constant $UNITS_XML = xml-data-flat('https://www.xbrl.org/utr/utr.xml','/utr:utr/utr:units/utr:unit', list('utr:unitId','utr:unitName','utr:nsUnit','utr:itemType','utr:itemTypeDate','utr:symbol','utr:definition','utr:baseStandard','utr:status','utr:versionDate','@id'),list('string','string','string','string','date','string','string','string','string','date','string'),dict(list('utr','http://www.xbrl.org/2009/utr')))


constant $CURRENCY_TAXONOMY = taxonomy('https://www.xbrl.org/taxonomy/int/currency/current/CR/2023-08-23/currency-elts.xsd')

constant $UTR_CURRENCY_CODES = set(for $unit in $UNITS_XML
                                    if $unit[3] == 'http://www.xbrl.org/2003/iso4217'
                                        $unit[1]
                                     else
                                        skip) 
constant $CURRENCY_NAMESPACE = 'https://xbrl.org/taxonomy/int/currency/current/CR/2023-08-23'

constant  $ISO_CURRENCY_CODES = $CURRENCY_TAXONOMY.concepts().local-name

/** $EXCLUDED_CURRENCIES  = list('BOV','CHE','CHW','CLF','COU','MXV','USN','UYI','XAG','XAU','XBA','XBB','XBC','XBD','XDR','XFU','XPD','XPT','XUA')**/

constant $EXCLUDED_CURRENCIES = ($UTR_CURRENCY_CODES - $ISO_CURRENCY_CODES).to-list

/** GET list of datatype models list('Monetary_per_Share','Monetary_per_Decimal','Monetary_per_Monetary','pure','shares','Volume_per_Duration','Energy_per_Duration') **/
constant $DATA_TYPE_MODELS = list(for $unit in $UNITS_XML
                                    if $unit[3] == none
                                        $unit 
                                    else
                                        skip
                                    )

constant $DATA_TYPES_THAT_ARE_UNITS = list(for $unit in $UNITS_XML
                                    if $unit[3] == 'http://www.xbrl.org/2003/instance'
                                        $unit 
                                    else
                                        skip
                                    ) 

constant $CURRENCY_UNITS = list(for $unit in $UNITS_XML
                                    if $unit[3] == 'http://www.xbrl.org/2003/iso4217' and $unit[1] not in $EXCLUDED_CURRENCIES
                                        $unit
                                     else
                                        skip)   

constant $ALL_CURRENCY_UNITS = list(for $unit in $UNITS_XML
                                    if $unit[3] == 'http://www.xbrl.org/2003/iso4217'
                                        $unit
                                     else
                                        skip)   

constant $ALT_CURRENCY_UNITS = list(for $unit in $UNITS_XML
                                    if $unit[1] in $EXCLUDED_CURRENCIES
                                        $unit
                                     else
                                        skip) 

constant $UTR_UNITS = list(for $unit in $UNITS_XML
                                    if $unit[3] == 'http://www.xbrl.org/2003/iso4217'  
                                        or ($unit[1] in filter $DATA_TYPE_MODELS returns $item[1]) 
                                        or ($unit[1] in filter $DATA_TYPES_THAT_ARE_UNITS returns $item[1])
                                            skip
                                     else
                                            $unit)                   



/** GET DATA TO CREATE CONCEPTS FOR MAGNITUDE 
---------------------------------------------**/

constant $SCALE_CONCEPTS = csv-data('https://raw.githubusercontent.com/xbrlus/xule/main/unitTests/source/xodel/scale.csv',true, list( 'string','string', 'decimal', 'decimal'))


/** GET DATA TO CREATE CONCEPTS FOR DOMAIN HEADERS 
---------------------------------------------**/

constant $DOMAIN_HEADERS = (list(
                                    list('UtrUnitsDomain','http://www.xbrl.org/2009/utr','utr-units', 'UTR Units [Domain]','utr'),
                                    list('AlternativeCurrencyDomain','http://www.xbrl.org/2003/iso4217','alt-currency-unit', 'UTR Units [Domain]','iso4217'),
                                    list('SIPrefixDomain','http://www.xbrl.org/2023/si-prefix','magnitude', 'SI Prefix [Domain]','si'),
                                    list('DatatypeUnitsDomain','http://www.xbrl.org/2023/datatype-units','datatype-units', 'Datatype Units [Domain]','datatype-unit'),
                                )
                            )

/** EXTRACT DATA FROM EXCEL FOR CONVERSION FACTORS 
-------------------------------------------------**/

constant $CONVERT_LINKS = excel-data('https://github.com/xbrlus/xule/raw/main/unitTests/source/xodel/unitRelations.xlsx', 'Sheet1',true,list('int','string','string', 'string','string','string','decimal'))

/** DEFINE THE PACKAGE 
-----------------------**/

constant $UNIT_TAXONOMY = 'UnitTaxonomy'


output create_base_unit_taxonomy
true
package-name $UNIT_TAXONOMY
package-url 'https://taxonomies.xbrl.us/xodel/Units'
document-uri 'units.xsd'
document-namespace 'http://www.xbrl.org/2023/units'
document-import list('https://www.xbrl.org/taxonomy/int/currency/current/CR/2023-08-23/currency-en.xsd').to-xodel
document-package-entry-point true
document-package-entry-point-description 'main entry point'
document-package-entry-point-language 'en'
document-package-entry-point-name 'Main'

/** DEFINE NEW LABEL ROLES 
-----------------------**/
output create_label_roles
true
package-name $UNIT_TAXONOMY
document-uri 'units.xsd'
role-uri 'http://www.xbrl.org/2023/role/symbol'
role-definition 'Defines the label role for the symbol associated with the unit'
role-used-on list(link:label).to-xodel

output create_label_roles_versionDate
true
package-name $UNIT_TAXONOMY
document-uri 'units.xsd'
role-uri 'http://www.xbrl.org/2023/role/versionDate'
role-definition 'Defines the label role for the version date associated with the unit'
role-used-on list(link:label).to-xodel


/** DEFINE NUMERATER DENOMINATOR and  ARC ROLE 
------------------------------**/
output create_operator_arcroles

$arcroles = list(list('http://www.xbrl.org/2002/arcrole/datatype-numerator','Defines the datatype numerator of a datatype that comprises two or more units for example flowItemType may have a numerator of volumeItemType.'),
list('http://www.xbrl.org/2023/arcrole/datatype-denominator','Defines the datatype denominator of a datatype that comprises two or more units for example flowItemType may have a denominator of timeItemType.'),
list('http://www.xbrl.org/2023/arcrole/datatype-multiplier','Defines the datatype multiplier of a datatype that comprises two or more units for example areaItemType may have a multiplier of lengthItemType.'),
list('http://www.xbrl.org/2023/arcrole/datatype-squared','Defines the datatype derived by squaring another datatype. For example area is derived from length squared.'),
list('http://www.xbrl.org/2023/arcrole/datatype-cubed','Defines the datatype derived by cubing another datatype. For example volume is derived from length cubed.'),
list('http://www.xbrl.org/2023/arcrole/unit-multiplied','Defines the unit multiplier of a unit that comprises two or more units for example mw may have a multiplier of w and m.'),
list('http://www.xbrl.org/2023/arcrole/unit-squared','Defines the unit derived by squaring another unit. For example square miles and miles.'),
list('http://www.xbrl.org/2023/arcrole/unit-cubed','Defines the unit derived by cubing another unit. For example cubic foot and feet.'),
list('http://www.xbrl.org/2023/arcrole/unit-numerator','Defines the unit numerator of another unit that comprises two or more units for example gallons per hour may have a numerator of gallons.'),
list('http://www.xbrl.org/2023/arcrole/unit-denominator','Defines the unit denominator of another unit that comprises two or more units for example gallons per hour may have a denominator of hour.'),
list('http://www.xbrl.org/2023/arcrole/converted-convertee','Defines the unit conversion factor to a unit from another unit. The conversion factor is defined as an attibute of the arc called conversionFactor'),
)
for $arc in $arcroles
    $arc 

package-name $UNIT_TAXONOMY
document-uri 'units.xsd'
arcrole-uri $arc[1]
arcrole-definition $arc[2]
arcrole-used-on list('Definition').to-xodel
arcrole-cycles-allowed 'none'

/** CREATE CONCEPTS 
-----------------------**/
output create_utr_concepts 
$baseUnit = $CURRENCY_TAXONOMY.concept(currency:ADP)
for $unit in $UTR_UNITS
    $unit

package-name $UNIT_TAXONOMY
document-uri 'utr-units.xsd'
document-imported-in 'units.xsd'
document-namespace 'http://www.xbrl.org/2009/utr'
concept $baseUnit.to-xodel
concept-id (if $rule-value[8]  == 'Customary' 'Non-SI' else  $rule-value[8]).lower-case + '_' + $rule-value[1]
concept-namespace 'http://www.xbrl.org/2009/utr'
concept-local-name $rule-value[1]

output create_alt_iso_concepts 
$baseUnit = $CURRENCY_TAXONOMY.concept(currency:ADP)
for $unit in $EXCLUDED_CURRENCIES
    $unit

package-name $UNIT_TAXONOMY
document-uri 'alt-currency-unit.xsd'
document-imported-in 'units.xsd'
document-namespace 'http://www.xbrl.org/2003/iso4217'
concept $baseUnit.to-xodel
concept-id  'iso4217_' + $rule-value
concept-namespace 'http://www.xbrl.org/2003/iso4217'
concept-local-name $rule-value


output create_datatype_unit_concepts 
$baseUnit = $CURRENCY_TAXONOMY.concept(currency:ADP)
for $unit in $DATA_TYPES_THAT_ARE_UNITS
    $unit

package-name $UNIT_TAXONOMY
document-uri 'datatype-units.xsd'
document-imported-in 'units.xsd'
document-namespace 'http://www.xbrl.org/2023/datatype-units'
concept $baseUnit.to-xodel
concept-id  'instance_' + $rule-value[1]
concept-namespace 'http://www.xbrl.org/2023/datatype-units'
concept-local-name $rule-value[1]


output create_scale_concepts 
$baseUnit = $CURRENCY_TAXONOMY.concept(currency:ADP)
$UniqueScale = (filter $SCALE_CONCEPTS returns $item[1]).to-set
for $unit in $UniqueScale
    $unit

package-name $UNIT_TAXONOMY
document-uri 'magnitude.xsd'
document-imported-in 'units.xsd'
document-namespace 'http://www.xbrl.org/2023/si-prefix'
concept $baseUnit.to-xodel
concept-id 'si-prefix_' + $rule-value
concept-namespace 'http://www.xbrl.org/2023/si-prefix'
concept-local-name $rule-value

/** CREATE DOMAIN ABSTRACTS 
-----------------------------**/

output create_domain_header_concepts 
$baseUnit = $CURRENCY_TAXONOMY.concept(currency:CurrencyDomain)
for $headers in $DOMAIN_HEADERS 
    $headers

package-name $UNIT_TAXONOMY
document-uri $rule-value[3] + '.xsd'
concept $baseUnit.to-xodel
concept-id  $rule-value[5] + '_' + $rule-value[1]
concept-namespace  $rule-value[2]
concept-local-name $rule-value[1]

/** IMPORT CURRENCY TAXONOMY -- THIS IS NEEDED SO THE CURRENCY TAXONOMY IS IN THE MODEL
-----------------------------**/
output import_currency_taxonomy
true

package-name $UNIT_TAXONOMY
import2-url 'https://www.xbrl.org/taxonomy/int/currency/current/CR/2023-08-23/currency-en.xsd'


/** CREATE LABELS 
-----------------------**/
output create_labels_standard 
    for $label in ($UTR_UNITS + $CURRENCY_UNITS)
        for $labelType in list(list(2,'http://www.xbrl.org/2003/role/label'), list(7,'http://www.xbrl.org/2003/role/documentation'),list(6,'http://www.xbrl.org/2023/role/symbol'),list(10,'http://www.xbrl.org/2023/role/versionDate'))
            $iso_temp_namespace = if $label[3] == 'http://www.xbrl.org/2003/iso4217'
                                    $CURRENCY_NAMESPACE
                                else
                                        $label[3]
            if ($labelType[1] in list(2,7) and $iso_temp_namespace == $CURRENCY_NAMESPACE)
                skip
            else
                if $label[$labelType[1]] == none
                    skip
                else
                    $label
package-name $UNIT_TAXONOMY
document-uri 'utr-units_lab.xml'
document-imported-in 'utr-units.xsd'
label-concept-name qname($iso_temp_namespace, $label[1]).to-xodel
label-text $label[$labelType[1]]
label-role $labelType[2]
label-lang 'en'

output add_alt_currency_labels 
    for $label in ($ALT_CURRENCY_UNITS)
            for $labelType in list(list(2,'http://www.xbrl.org/2003/role/label'), list(7,'http://www.xbrl.org/2003/role/documentation'),list(6,'http://www.xbrl.org/2023/role/symbol'),list(10,'http://www.xbrl.org/2023/role/versionDate'))
                if $label[6] == none and $labelType[1] == 6
                    skip
                else
                    $label
package-name $UNIT_TAXONOMY
document-uri 'alt-currency-unit_lab.xml'
document-imported-in 'alt-currency-unit.xsd'
label-concept-name qname($label[3], $label[1]).to-xodel
label-text $label[$labelType[1]]
label-role $labelType[2]
label-lang 'en'

output add_datatype_unit_labels 
    for $label in ($DATA_TYPES_THAT_ARE_UNITS)
            for $labelType in list(list(2,'http://www.xbrl.org/2003/role/label'), list(7,'http://www.xbrl.org/2003/role/documentation'))
                    $label
package-name $UNIT_TAXONOMY
document-uri 'datatype-units_lab.xml'
document-imported-in 'datatype-units.xsd'
label-concept-name qname('http://www.xbrl.org/2023/datatype-units', $label[1]).to-xodel
label-text $label[$labelType[1]]
label-role $labelType[2]
label-lang 'en'

output create_domain_labels
    for $label in $DOMAIN_HEADERS
        $label
package-name $UNIT_TAXONOMY
document-uri $label[3] + '_lab.xml'
label-concept-name qname($label[2], $label[1]).to-xodel
label-text $label[4]
label-role 'http://www.xbrl.org/2003/role/label'
label-lang 'en'

output create_magnatude_labels
    for $label in $SCALE_CONCEPTS
        for $labelType in list(list(1,'http://www.xbrl.org/2003/role/label'),list(2,'http://www.xbrl.org/2023/role/symbol'))
                $label
package-name $UNIT_TAXONOMY
document-uri 'magnitude_lab.xml'
document-imported-in 'magnitude.xsd'
label-concept-name qname('http://www.xbrl.org/2023/si-prefix', $label[1]).to-xodel
label-text $label[$labelType[1]]
label-role $labelType[2]
label-lang 'en'

output create_magnatude_documentation
    for $label in $SCALE_CONCEPTS
                $label
package-name $UNIT_TAXONOMY
document-uri 'magnitude_lab.xml'
document-imported-in 'magnitude.xsd'
label-concept-name qname('http://www.xbrl.org/2023/si-prefix', $label[1]).to-xodel
label-text 'A ' + $label[1] + ' is the SI unit prefix represented using the symbol ' + $label[2] + '. A ' + $label[1] + ' uses a base 10 magnitude of ' + $label[3] + '. '
label-role 'http://www.xbrl.org/2003/role/documentation'
label-lang 'en'


/** CREATE PRESENTATIONS 
------------------------**/

output create_utr_presentation_role
    true

package-name $UNIT_TAXONOMY
document-uri 'utr-units.xsd'
role-uri 'https://www.xbrl.org/2023/roles/utr-presentation'
role-definition 'Units List'
role-used-on list('Presentation').to-xodel


output create_utr_presentation
$FirstItem = qname('http://www.xbrl.org/2009/utr','UtrUnitsDomain')
$UTR_units_Length = $UTR_UNITS.length
for $child_no in range($UTR_units_Length)
    $record = $UTR_UNITS[$child_no]
    $record

package-name $UNIT_TAXONOMY
document-uri 'utr-units_pre.xml'
document-imported-in 'utr-units.xsd'
relationship-source $FirstItem.to-xodel
relationship-target qname('http://www.xbrl.org/2009/utr', $record[1]).to-xodel
relationship-order $child_no
relationship-role 'utr-presentation'
relationship-arcrole 'parent-child'
relationship-type 'presentation'

/** --------------------**/

output create_magnitude_presentation_role
    true

package-name $UNIT_TAXONOMY
document-uri 'magnitude.xsd'
role-uri 'https://www.xbrl.org/2023/roles/si-prefix-presentation'
role-definition 'SI Prefix List'
role-used-on list('Presentation').to-xodel


output create_magnitude_presentation
$FirstItem = qname('http://www.xbrl.org/2023/si-prefix','SIPrefixDomain')
$UTR_units_Length = $SCALE_CONCEPTS.length
for $child_no in range($UTR_units_Length)
    $record = $SCALE_CONCEPTS[$child_no]
    $record

package-name $UNIT_TAXONOMY
document-uri 'magnatude_pre.xml'
document-imported-in 'magnitude.xsd'
relationship-source $FirstItem.to-xodel
relationship-target qname('http://www.xbrl.org/2023/si-prefix', $record[1]).to-xodel
relationship-order $child_no
relationship-role 'si-prefix-presentation'
relationship-arcrole 'parent-child'
relationship-type 'presentation'

/** --------------------**/

output create_currency_presentation_role
    true

package-name $UNIT_TAXONOMY
document-uri 'units.xsd'
role-uri 'https://www.xbrl.org/2023/roles/currency-presentation'
role-definition 'Currency List'
role-used-on list('Presentation').to-xodel


output create_currency_presentation
$FirstItem = qname($CURRENCY_NAMESPACE,'CurrencyDomain')
$UTR_units_Length = $CURRENCY_UNITS.length
for $child_no in range($UTR_units_Length)
    $record = $CURRENCY_UNITS[$child_no]
    $record

package-name $UNIT_TAXONOMY
document-uri 'units_pre.xml'
document-imported-in 'units.xsd'
relationship-source $FirstItem.to-xodel
relationship-target qname($CURRENCY_NAMESPACE, $record[1]).to-xodel
relationship-order $child_no
relationship-role 'currency-presentation'
relationship-arcrole 'parent-child'
relationship-type 'presentation'

/** --------------------**/

output create_datatype_presentation_role
    true

package-name $UNIT_TAXONOMY
document-uri 'units.xsd'
role-uri 'https://www.xbrl.org/2023/roles/datatype-presentation'
role-definition 'Data Type List'
role-used-on list('Presentation').to-xodel


output create_datatype_presentation
$FirstItem = qname('http://www.xbrl.org/2023/datatype-units','DatatypeUnitsDomain')
$datatype_Length = $DATA_TYPES_XML.length
for $child_no in range($datatype_Length)
    $record = $DATA_TYPES_XML[$child_no]
    $record

package-name $UNIT_TAXONOMY
document-uri 'units_pre.xml'
relationship-source $FirstItem.to-xodel
relationship-target qname($record[1], $record[2]).to-xodel
relationship-order $child_no
relationship-role 'datatype-presentation'
relationship-arcrole 'parent-child'
relationship-type 'presentation'

/** SHow datatype as a parent of the standard unit **/
output link_unit_to_datatype
$UTR_units_Length = $UTR_UNITS.length
for $child_no in range($UTR_units_Length)
    $utr_unit = $UTR_UNITS[$child_no]
    $source = qname('http://www.xbrl.org/dtr/type/2022-03-31',$utr_unit[4])
    $utr_unit

package-name $UNIT_TAXONOMY
document-uri 'units_pre.xml'
relationship-source $source.to-xodel
relationship-target qname('http://www.xbrl.org/2009/utr', $utr_unit[1]).to-xodel
relationship-order $child_no
relationship-role 'utr-presentation'
relationship-arcrole 'parent-child'
relationship-type 'presentation'

/** SHow datatype as a parent of the standard unit **/
output link_unit_to_builtIndatatype
$UTR_units_Length = $DATA_TYPES_THAT_ARE_UNITS.length
for $child_no in range($UTR_units_Length)
    $utr_unit = $DATA_TYPES_THAT_ARE_UNITS[$child_no]
    $source = qname('http://www.xbrl.org/dtr/type/2022-03-31',$utr_unit[4])
    $utr_unit

package-name $UNIT_TAXONOMY
document-uri 'units_pre.xml'
relationship-source $source.to-xodel
relationship-target qname('http://www.xbrl.org/2023/datatype-units', $utr_unit[1]).to-xodel
relationship-order $child_no
relationship-role 'utr-presentation'
relationship-arcrole 'parent-child'
relationship-type 'presentation'

output link_currency_to_datatype

$CUR_units_Length = ($CURRENCY_UNITS + $ALT_CURRENCY_UNITS).length
$Currancy_Units_Sort = ($CURRENCY_UNITS + $ALT_CURRENCY_UNITS).sort('asc')
for $cur_child_no in range($CUR_units_Length)
    $CUR_unit = $Currancy_Units_Sort[$cur_child_no]
    $source = qname('http://www.xbrl.org/dtr/type/2022-03-31',$CUR_unit[4])
    $current_namspace = if $CUR_unit[1] in $EXCLUDED_CURRENCIES $CUR_unit[3] else $CURRENCY_NAMESPACE
    $CUR_unit

package-name $UNIT_TAXONOMY
document-uri 'units_pre.xml'
relationship-source $source.to-xodel
relationship-target qname($current_namspace, $CUR_unit[1]).to-xodel
relationship-order $cur_child_no
relationship-role 'utr-presentation'
relationship-arcrole 'parent-child'
relationship-type 'presentation'


/** CREATE DEFINITIONS 
------------------------**/

output create_utr_definition_role
    true

package-name $UNIT_TAXONOMY
document-uri 'utr-units.xsd'
role-uri 'https://www.xbrl.org/2023/roles/utr-conversion'
role-definition 'Units Conversion Factors'
role-used-on list('Definition').to-xodel

output link_unit_to_unit

for $links in $CONVERT_LINKS
    $source = qname('http://www.xbrl.org/2009/utr',$links[2])
    $targetqnames = to-qname($links[6] + ':' + $links[5])
    $links

package-name $UNIT_TAXONOMY
document-uri 'units_def.xml'
document-imported-in 'units.xsd'
relationship-source $source.to-xodel
relationship-target  $targetqnames.to-xodel
relationship-order $links[1]
relationship-attributes if $links[7] == 0 dict(list()).to-xodel else dict(list(utr:ConversionRate,$links[7].string)).to-xodel
relationship-role 'utr-conversion'
relationship-arcrole  $links[4]
relationship-type 'definition'


/** Add References for qnames **/

output add_reference_qname

for $ref in ($UTR_UNITS + $CURRENCY_UNITS)
        $iso_temp_namespace = if $ref[3] == 'http://www.xbrl.org/2003/iso4217'
                                    $CURRENCY_NAMESPACE
                                else
                                        $ref[3]
        $ref

package-name $UNIT_TAXONOMY
document-uri 'units_ref.xml'
document-imported-in 'units.xsd'
reference-role 'https://xbrl.org/2022/role/property'
reference-concept-name qname($iso_temp_namespace, $ref[1]).to-xodel
reference-parts  list(list(property-part:StatusRemark, qname($ref[3],$ref[1]))).to-xodel


output add_reference_qname_alt

for $ref in ($ALT_CURRENCY_UNITS)
        $ref

package-name $UNIT_TAXONOMY
document-uri 'units_ref.xml'
document-imported-in 'units.xsd'
reference-role 'https://xbrl.org/2022/role/property'
reference-concept-name qname($ref[3], $ref[1]).to-xodel
reference-parts  list(list(property-part:StatusRemark, qname($ref[3],$ref[1]))).to-xodel

output add_reference_qname_datatype

for $ref in ($DATA_TYPES_THAT_ARE_UNITS)
        $ref

package-name $UNIT_TAXONOMY
document-uri 'units_ref.xml'
document-imported-in 'units.xsd'
reference-role 'https://xbrl.org/2022/role/property'
reference-concept-name qname('http://www.xbrl.org/2023/datatype-units', $ref[1]).to-xodel
reference-parts  list(list(property-part:StatusRemark, qname($ref[3],$ref[1]))).to-xodel


output add_reference_qname_si

for $ref in ($SCALE_CONCEPTS)
        $ref

package-name $UNIT_TAXONOMY
document-uri 'magnitude_ref.xml'
document-imported-in 'units.xsd'
reference-role 'https://xbrl.org/2022/role/property'
reference-concept-name qname('http://www.xbrl.org/2023/si-prefix', $ref[1]).to-xodel
reference-parts  list(list(property-part:StatusRemark, qname('http://www.xbrl.org/2023/si-prefix',$ref[1]))).to-xodel
